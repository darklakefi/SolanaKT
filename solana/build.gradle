plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'maven-publish'
}

group 'com.solana'
version '2.1.1'

android {
    namespace 'com.solana.solana'
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 36
        versionCode 1
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // read RPC URL properties fro testing
        def defaultRpcUrl = project.properties["testing.rpc.defaultUrl"]
        def rpcUrl = project.properties["rpcUrl"] ?: defaultRpcUrl

        def useLocalValidator = project.properties["localValidator"].toBoolean()
        def localRpcUrl = project.properties["testing.rpc.localUrl"]
        if (useLocalValidator && localRpcUrl != null) rpcUrl = localRpcUrl

        // set RPC URL (used in testing)
        buildConfigField 'String', 'RPC_URL', "\"${rpcUrl.toString().replace('\"', '')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }
    
    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'org.bitcoinj:bitcoinj-core:0.16.2'
    implementation 'net.i2p.crypto:eddsa:0.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.0'

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(layout.buildDirectory.file("outputs/aar/solana-release.aar"))  {
                builtBy tasks.getByName("assemble")
            }
            artifactId 'solana'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/metaplex-foundation/SolanaKT"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}